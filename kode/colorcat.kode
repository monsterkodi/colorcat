###
 0000000   0000000   000       0000000   00000000    0000000   0000000   000000000
000       000   000  000      000   000  000   000  000       000   000     000   
000       000   000  000      000   000  0000000    000       000000000     000   
000       000   000  000      000   000  000   000  000       000   000     000   
 0000000   0000000   0000000   0000000   000   000   0000000  000   000     000   
###

klor = require 'klor'
karg = require 'karg'

kolor = klor.kolor
kolor.globalize()

NEWLINE = /\r?\n/

#  0000000   00000000    0000000    0000000
# 000   000  000   000  000        000     
# 000000000  0000000    000  0000  0000000 
# 000   000  000   000  000   000       000
# 000   000  000   000   0000000   0000000 

rpad = (s, l) ->
    s = String s
    while s.length < l then s += ' '
    s

text = 
    white:   'w'
    cyan:    'c'
    magenta: 'm'
    blue:    'b'
    yellow:  'y'
    green:   'g'
    red:     'r' 
        
textColors = ''
for c,s of text
    
    shorts = ''
    switch s
        when 'x''z' then
        else
            for i in [1..8]
                shortf = s+i
                shorts += kolor[shortf] ' ' + shortf + ' '
    
    textColors += "    #{c}  . ? #{kolor[c] '██'}#{dim kolor[c] '██'}#{shorts} . = false . - #{text[c]}\n"

bgrd = 
    bgRed:     'R' 
    bgGreen:   'G'
    bgYellow:  'Y'
    bgBlue:    'B'
    bgMagenta: 'M'
    bgCyan:    'C'
    bgWhite:   'W'
    
bgfunc = (c) ->
    s = bgrd[c]
    switch s
        when 'Z' then 'W2'
        when 'W' then 'W4'
        else s+4
    
bgrdColors = ''
for c,s of bgrd
    
    shorts = ''
    switch s
        when 'W'
            for i in [1..8]
                shortb = 'W'+i
                shortf = 'w'.toLowerCase()+(9-i)
                shorts += kolor[shortf] kolor[shortb] ' ' + shortb + ' '
            
        when 'Z' then
            
        else
            for i in [1..8]
                shortb = s+i
                shortf = s.toLowerCase()+(9-i)
                shorts += kolor[shortf] kolor[shortb] ' ' + shortb + ' '
    
    bgrdColors += "    #{c}  . ? #{reset(kolor[bgfunc c]("    "))}#{shorts} . = false . - #{bgrd[c]}\n"

args = karg """
colorcat
    file         the file(s) to display or stdin    **
    fat          . ? #{ gray 'bold                                ' }      . = false  
    dim          . ? #{ dim white '  ▼▼ dim                            ' } . = false
#{textColors}
#{bgrdColors}
    ext          use syntax highlighting for *.ext        
    pattern      colorize with pattern
    patternFile  colorize with patterns in file     -P
    skipEmpty    skip empty lines                   = false
    lineNumbers  prepend output with line numbers   = false
    debug                                           = false -X
    
version   #{require("#{__dirname}/../package.json").version}
"""

if args.debug
    noon_stringify = require 'noon/js/stringify'
    log noon_stringify args, colors:true
    
#  0000000   0000000   000       0000000   00000000   000  0000000  00000000
# 000       000   000  000      000   000  000   000  000     000   000     
# 000       000   000  000      000   000  0000000    000    000    0000000 
# 000       000   000  000      000   000  000   000  000   000     000     
#  0000000   0000000   0000000   0000000   000   000  000  0000000  00000000

colorize = (str, stack) ->
    try
        spl = stack.map (s) -> String(s).split '.'
        spl = spl.flat()

        if not ('keep' in spl)
            for s in spl
                if s.substr(0,2) == 's:'
                    str = s.substr(2)
                    spl = spl.filter (s) -> s.substr(0,2) != 's:'
                    break

        for n in spl
            if kolor[n]?
                str = kolor[n] str
            else if kolor[bgfunc n]?
                str = kolor[bgfunc n] str
    catch err
        error err
    str

# 00000000  000   000  00000000    0000000   000   000  0000000  
# 000        000 000   000   000  000   000  0000  000  000   000
# 0000000     00000    00000000   000000000  000 0 000  000   000
# 000        000 000   000        000   000  000  0000  000   000
# 00000000  000   000  000        000   000  000   000  0000000  

regexes = []

clrlst = Object.assign {}, bgrd
clrlst = Object.assign clrlst, text
fbgnms = kolor.FG_NAMES.concat kolor.BG_NAMES
fbgcol = kolor.FG_COLORS.concat kolor.BG_COLORS
cnames = Object.keys(text).concat Object.keys(bgrd)
cnames = cnames.concat fbgnms
invert = {}
for k,v of clrlst then invert[v] = k
invert.f = 'bold'
invert.d = 'dim'
invert.k = 'keep'
invert.x = 'gray'
invert.z = 'w2'
invert.Z = 'W1'

expand = (e) ->
    
    expd = (c) ->
        if c[0] in fbgcol
            if c[0..1] in fbgnms
                if c.length == 2 then return c
                else                  return c[0..1] + '.' + expd c[2..]
                
        if c?.split? and c not in cnames
            s = c.split 's\:'
            if invert[s[0][0]]
                if s[0].length == 1 then r = invert[s[0][0]]
                else                     r = invert[s[0][0]] + '.' + expd s[0][1..]
            else
                # error 'dafuk?', c, s
                r = ''
            r += '.s:' + s[1] if s.length > 1
            r
        else
            c
    
    for pat,cls of e
        if cls instanceof Array
            e[pat] = cls.map (clr) -> expd clr.split('.')[0]
        else
            e[pat] = expd cls
    e 

# 00000000  000   000  000   000  000   000  000   000
# 000       000   000  0000  000  000  000    000 000 
# 000000    000   000  000 0 000  0000000      00000  
# 000       000   000  000  0000  000  000      000   
# 000        0000000   000   000  000   000     000   

funkyText = (s) -> s
funkyBgrd = (s) -> s

for c in Object.keys text
    if args[c]
        funkyText = kolor[c]
        
for c in Object.keys bgrd
    if args[c]
        funkyBgrd = kolor[bgfunc c]

if args.fat
    fatText = (s) -> bold funkyText s
else
    fatText = funkyText

if args.dim
    dimText = (s) -> dim fatText s
else
    dimText = fatText

# 00000000    0000000   000000000  000000000  00000000  00000000   000   000   0000000
# 000   000  000   000     000        000     000       000   000  0000  000  000     
# 00000000   000000000     000        000     0000000   0000000    000 0 000  0000000 
# 000        000   000     000        000     000       000   000  000  0000       000
# 000        000   000     000        000     00000000  000   000  000   000  0000000 

patternFunc = ->
    
    loadSyntax = (f) ->
        fs = require 'fs'
        if fs.existsSync f
            noon_load = require 'noon/js/load'
            expand noon_load f 
        else
            error "can't locate syntax file #{f}"
    
    if args.pattern?
        noon_parse = require 'noon/js/parse'
        patterns = expand noon_parse args.pattern
        # log patterns
    else if args.patternFile?
        patterns = loadSyntax args.patternFile
    
    if not patterns?
        return (chunk) -> funkyBgrd dimText chunk
        
    matchr = require './matchr'
    matchrConfig = matchr.config patterns
          
    pattern = (chunk) ->
        chunk = kolor.strip chunk
        rngs = matchr.ranges matchrConfig, chunk
        diss = matchr.dissect rngs
        
        if diss.length
            for di in [diss.length-1..0]
                d = diss[di]
                clrzd = colorize d.match, d.stack.reverse()
                chunk = chunk.slice(0, d.start) + clrzd + chunk.slice(d.start+d.match.length)
        chunk
        
    return pattern

#  0000000  000000000  00000000   00000000   0000000   00     00
# 000          000     000   000  000       000   000  000   000
# 0000000      000     0000000    0000000   000000000  000000000
#      000     000     000   000  000       000   000  000 0 000
# 0000000      000     000   000  00000000  000   000  000   000

colorStream = (stream, pattern) ->

    lineno = 0
    buffer = ''
    stream.on 'data' (chunk) ->
        
        if not chunk.endsWith '\n'
            buffer += chunk
            return
            
        lines = (buffer+chunk).split '\n'
        colorLines = lines.map (l) -> pattern l
        
        if args.skipEmpty
            colorLines = colorLines.filter (l) -> 
                kolor.strip(l).length > 0
                
        if args.lineNumbers
            colorLines = colorLines.map (l) -> 
                lineno += 1
                return gray(dim rpad("#{lineno}",6)) + l
                
        log colorLines.join '\n'
        buffer = ''
        
syntaxStream = (stream, ext) ->

    lineno = 0
    buffer = ''
    stream.on 'data' (chunk) ->
        
        if not chunk.endsWith '\n'
            buffer += chunk
            return
        
        lines = (buffer+chunk).split '\n'
        colorLines = []
        
        rngs = klor.dissect lines, ext
        for index in [0...lines.length]
            colorLines.push klor.kolorizeChunks chunks:rngs[index], number:args.lineNumbers and index+1
        
        if args.skipEmpty
            colorLines = colorLines.filter (l) -> 
                kolor.strip(l).length > 0
                
        log colorLines.join '\n'
        buffer = ''
    
###
 0000000   0000000   000000000    
000       000   000     000       
000       000000000     000       
000       000   000     000       
 0000000  000   000     000       
###
    
if args.file.length
  
    slash = require 'kslash'
    
    for file in args.file    

        text = slash.readText file
  
        log klor.syntax text:text, ext:slash.ext(file), numbers:args.lineNumbers
                
else
    
    process.stdin.setEncoding 'utf8'
    
    if args.ext and args.ext in klor.exts
        syntaxStream process.stdin, args.ext
    else    
        colorStream process.stdin, patternFunc()

# process.on 'exit', (code) -> ▸end 'colorcat'
